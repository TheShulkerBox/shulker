#> Handles joining duels via trigger
# When you attempt 

from server:core import SERVER_TICK, SERVER_LOAD
from lobby:core import LOBBY_TEAM

from ../instance import INSTANCE_META, INSTANCE_ID, new_instance_unsafe
from ./models import DuelsMap
from ./lobby import DUELS_LOBBY_LOCATION
from ./instance import BELOW_NAME_DISPLAY, select_instance_players

JOIN_DUEL_TRIGGER = Scoreboard("join_duel", criteria="trigger")


# Check player count in selected duels map (selected by trigger command when requesting to join)
# Reference duels occupancy data structure (dictionary?) for appropriate map
append function SERVER_TICK:
    execute function ./detect_join:
        JOIN_DUEL_TRIGGER[f"@a[team={LOBBY_TEAM}]"].enable()  

        # detect if a player has used the trigger
        execute as @a[scores={JOIN_DUEL_TRIGGER=1..}] run function ~/attempt:
            # cache value in temp and reset trigger (just incase we early return)
            SCORE.temp["#join_duel_id"] = JOIN_DUEL_TRIGGER["@s"]
            JOIN_DUEL_TRIGGER["@s"].reset()
            
            # grab the map from the duel trigger number 
            requested_map = DuelsMap.get(hashed_id=SCORE.temp["#join_duel_id"])

            # if there is no map, someone manually typed `/trigger join_duel`
            if requested_map == false:
                return run tellraw @s [
                    {text: "Cannot join duel arena with id ", color: Theme.Failure},
                    SCORE.temp["#join_duel_id"]  # auto converts to text component
                ]

            # if there is not an instance running on the map, auto join
            # otherwise, continue
            instance = INSTANCE_META.get(map_id=requested_map.id)
            if instance == false:
                return run function ~/create_instance:
                    # create a new instance, assign it to @s, join arena
                    INSTANCE_ID["@s"] = new_instance_unsafe(requested_map)
                    SCORE.temp["#instance_player_count"] = 1
                    function ./join_instance with var requested_map
            
            # if already in duel, do nothing
            if instance.id == INSTANCE_ID["@s"]:
                return run tellraw @s {text: "You can't join a duel arena that you are already in!", color: Theme.Failure}

            # count the players in the instance
            with select_instance_players(instance.id) as instance_tag:
                execute store result score var SCORE.temp["#instance_player_count"] if entity @a[tag=instance_tag]

            # compare whether duels lobby is full or not
            if SCORE.temp["#instance_player_count"] < requested_map.max_player_count:
                INSTANCE_ID["@s"] = instance.id
                function ./join_instance with var requested_map
            else:
                tellraw @s {text: "That arena is full!", color: Theme.Failure}


function ./join_instance:
    """Actions that run when a player joins an instance.
    
    as @s: player
    macro arguments from `DuelsMap` ($(name), $(id), etc.)
    """
    execute positioned DUELS_LOBBY_LOCATION run tp @s ~ ~ ~
    scoreboard players display name @s BELOW_NAME_DISPLAY {text: $(name: string), color: Theme.Secondary}
    title @s actionbar {text: "Joined arena!", color: Theme.Success, bold: true}
    execute positioned DUELS_LOBBY_LOCATION run playsound minecraft:block.note_block.bit master @s ~ ~ ~ 2 1 1
    tellraw @a [
        {text: "", click_event: {action: "run_command", command: f"/trigger join_duel set {$(hashed_id)}"}},
        {selector: "@s"},
        {text: " has joined the ", color: Theme.Body},
        {text: $(name: string), color: Theme.Secondary},
        {text: " dueling arena.\nClick here to duel!", color: Theme.Body},
    ]
