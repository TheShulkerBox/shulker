#> Displays flying damage text
# When you hit a player, the amount of damage you deal flies out of the entity
# with random motion.
#
# Inspired from @itsduck__ from mcc
from server:services/item_dropping import ITEM_PROCESSED_TAG
from lib:flying_text import FlyingText


DAMAGE_TAKEN = Scoreboard("player.damage_taken", criteria="minecraft.custom:minecraft.damage_resisted")
damage_text = FlyingText(strength=(0.2, 0.4, 0.2))


advancement ./on_hurt {
    "criteria": {
        "on_hurt": { "trigger": "minecraft:entity_hurt_player" }
    },
    "rewards": { "function": (./on_hurt) },
}


function ./on_hurt:
    advancement revoke @s only ./on_hurt
    
    tellraw @s DAMAGE_TAKEN["@s"]
    SCORE.temp["#hearts_damaged"] = DAMAGE_TAKEN["@s"] / 2
    execute positioned ~ ~1.6 ~:
        with damage_text.summon({damage_taken: SCORE.temp["#hearts_damaged"]}) as self:
            data modify var self.text set value {text: $(damage_taken: string), color: "#af2929"}
            self.transformation.scale[0] += min(1.5, SCORE.temp["#hearts_damaged"] / 25)
            self.transformation.scale[1] += min(1.5, SCORE.temp["#hearts_damaged"] / 25)
            self.transformation.scale[2] += min(1.5, SCORE.temp["#hearts_damaged"] / 25)
    
    DAMAGE_TAKEN["@s"].reset()
