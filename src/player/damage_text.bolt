#> Displays flying damage text
# When you hit a player, the amount of damage you deal flies out of the entity
# with random motion.
#
# Inspired from @itsduck__ from mcc
from server:services/item_dropping import ITEM_PROCESSED_TAG
from lib:flying_text import FlyingText

DAMAGE_TAKEN = Scoreboard("player.damage_taken", criteria="minecraft.custom:minecraft.damage_taken")
damage_text = FlyingText(strength=(0.2, 0.35, 0.2))


advancement ./on_hurt {
    "criteria": { "on_hurt": { "trigger": "minecraft:entity_hurt_player" } },
    "rewards": { "function": (./on_hurt) },
}


function ./on_hurt:
    advancement revoke @s only ./on_hurt
    
    # convert to hearts (round) (`@s` needs to resolve here)
    SCORE.temp["#hearts_damaged"] = DAMAGE_TAKEN["@s"] / 10

    # make the text fly from further up
    execute positioned ~ ~1.8 ~:
        with damage_text.summon({damage_taken: SCORE.temp["#hearts_damaged"]}) as self:
            # directly macro the damage taken into the string
            data modify var self.text set value {text: $(damage_taken: string), color: "#af2929", bold: true}

            # scale the text display based on the number of hearts damaged. We cap it at max 2x scale
            self.transformation.scale[0] += min(2, SCORE.temp["#hearts_damaged"] / 10)
            self.transformation.scale[1] += min(2, SCORE.temp["#hearts_damaged"] / 10)
            self.transformation.scale[2] += min(2, SCORE.temp["#hearts_damaged"] / 10)
    
    # reset to avoid accumulation
    DAMAGE_TAKEN["@s"].reset()
